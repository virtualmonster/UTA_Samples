grammar com.hcl.products.onetest.lang.script.Script with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import 'http://www.eclipse.org/xtext/xbase/Xbase' as xbase
generate script "http://www.hcl.com/products/onetest/lang/script/Script"

Model:
	{Model}
	('note' ':' note=STRING)?
	('disabled' ':' disabled=BOOLEAN)?
	('handlers' ':' handlers+=Action* | 'steps' ':' steps=Body)
	('values' ':' values+=Values*)?
	('macros' ':' macros+=Macros*)?
	('messages' ':' messages+=Messages*)?
	('blobs' ':' blobs+=Blobs*)?;

BOOLEAN returns ecore::EBoolean:
	'true' | 'false';

Action returns xbase::XExpression:
	BEGIN
		'-' (CallMacro | Pause)
	END;

//TODO change this to a reference to another ECore model so EMF can auto resolve?
CallMacro:
	'do' URL=STRING;

Pause:
	'pause' duration=INT unit=TimeUnits;

enum TimeUnits:
	MILLI_SECOND='ms' | SECOND='s' | MINUTE='m' | HOUR='h';

// TODO http://20000frames.blogspot.com/2010/09/working-with-emaps-in-xtext.html
Messages returns StringToMessageMapEntry:
	key=ID ':' value=Message;

Message:
	('payload' ':' payload=STRING)?;

Macros returns StringToMacroMapEntry:
	BEGIN
		key=ID ':'  value=Macro
	END;

Macro:
	BEGIN
		('note' ':' note=STRING)?
		('steps' ':' steps=Body)?
		('values' ':' values+=Values*)?
	END;

Body returns xbase::XBlockExpression:
	{XBlockExpression}
	(expressions+=Action*)?;

Values: //; returns ecore::EStringToStringMapEntry:
	key=ID ':' value=STRING;

Blobs: //; returns ecore::EStringToStringMapEntry:
	key=ID ':' value=STRING;

// synthetic tokens used for  indentation-aware blocks
terminal BEGIN:
	'synthetic:BEGIN'; // increase indentation

terminal END:
	'synthetic:END'; // decrease indentation	